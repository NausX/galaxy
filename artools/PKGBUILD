# Maintainer: artoo <artoo@manjaro.org>

_url="https://github.com/artix-linux/iso-profiles"
_profiles=iso-profiles

_tbuild=true
_pbuild=true

pkgbase=artools
if $_tbuild;then
    _pkgname=('artools-base' 'artools-pkg' 'artools-yaml' 'artools-iso')
    _source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
else
    _pkgname=()
    _source=()
fi
if $_pbuild;then
    _pkgname+=('artools-iso-base' 'artix-iso-profiles')
    _source+=("${_profiles}-${pkgver}.tar.gz::$_url/archive/${pkgver}.tar.gz")
fi
pkgname=("${_pkgname[@]}")
pkgver=0.4
pkgrel=1
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools')
url='https://github.com/artix-linux/artools'
makedepends=('docbook2x')
if $_tbuild;then
    _source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
else
    _source=()
fi
if $_pbuild;then
    _source+=("${_profiles}-${pkgver}.tar.gz::$_url/archive/${pkgver}.tar.gz")
fi
source=("${_source[@]}")
sha256sums=('8e9ef3ed4db01f14b5467e841443ca7efd911f3c550fa22d6be19226145e7a00'
            'b65628ee55f88dfa307406e0872434b04eee2ed09dc616db9c714320068c61dc')

prepare() {
    if $_tbuild;then
        cd ${srcdir}/${pkgbase}-${pkgver}
        sed -e "s/^Version=.*/Version=${pkgver}/" -i Makefile
    fi
    if $_pbuild;then
        cd ${srcdir}/${_profiles}-${pkgver}
        sed -e "s/^Version=.*/Version=${pkgver}/" -i Makefile
    fi
    # patches here
}

build() {
    if $_tbuild;then
        cd ${srcdir}/${pkgbase}-${pkgver}
        make SYSCONFDIR=/etc PREFIX=/usr
    else
        echo
    fi
}

package_artools-base() {
    pkgdesc='Development tools for Artix (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools'
                'artools-yaml: Artix yaml tools')
    backup=('etc/artools/artools.conf')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg() {
    pkgdesc='Development tools for Artix (packaging tools)'
    depends=('artools-base' 'namcap' 'wget')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-yaml() {
    pkgdesc='Development tools for Artix (yaml tools)'
    depends=('artools-base' 'artools-iso-base')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_yaml
}

package_artools-iso() {
    pkgdesc='Development tools for Artix (ISO tools)'
    depends=('artools-yaml' 'dosfstools' 'libisoburn' 'squashfs-tools' 'mkinitcpio' 'mktorrent' 'grub')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

package_artools-iso-base() {
    pkgdesc='Development tools for Artix (ISO base profile)'

    cd ${srcdir}/${_profiles}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artix-iso-profiles() {
    pkgdesc='Artix iso profiles'
    depends=("artools-yaml>=0.2")

    cd ${srcdir}/${_profiles}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_profiles
}

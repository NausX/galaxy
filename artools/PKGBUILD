# Maintainer: artoo <artoo@manjaro.org>

_url="https://github.com/artix-linux/iso-profiles"
_profiles=iso-profiles

pkgbase=artools
pkgname=(
        'artools-base'
        'artools-iso-base'
        'artools-pkg'
        'artools-iso'
        'artools-yaml'
        'artix-iso-profiles'
        )
pkgver=0.3
pkgrel=1
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools')
url='https://github.com/artix-linux/artools'
makedepends=('docbook2x')
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
        "${_profiles}-${pkgver}.tar.gz::$_url/archive/${pkgver}.tar.gz")
sha256sums=('1ed85639fc00f2d8769143397eed15393a791208a8f6f06bb8b21125dcef80eb'
            '83360213399a78731644f8cd882eaa684e955363b1273910d54bd1d334cc80a4')

_inst_dir(){
    install -d $pkgdir/usr/share/artools/iso-profiles
    cp -a $srcdir/${_profiles}-${pkgver}/$1 $pkgdir/usr/share/artools/iso-profiles/
}

prepare() {
    cd ${pkgbase}-${pkgver}
    # patches here
    sed -e "s/^Version=.*/Version=${pkgver}/" -i Makefile
}

build() {
    cd ${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr
}

package_artools-base() {
    pkgdesc='Development tools for Artix (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools'
                'artools-yaml: Artix yaml tools')
    backup=('etc/artools/artools.conf')

    cd ${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg() {
    pkgdesc='Development tools for Artix (packaging tools)'
    depends=('artools-base' 'namcap')

    cd ${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-yaml() {
    pkgdesc='Development tools for Artix (yaml tools)'
    depends=('artools-base' 'iso-profiles-base')

    cd ${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_yaml
}

package_artools-iso() {
    pkgdesc='Development tools for Artix (ISO tools)'
    depends=('artools-yaml' 'dosfstools' 'libisoburn' 'squashfs-tools' 'mkinitcpio' 'mktorrent' 'grub')

    cd ${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

package_artools-iso-base() {
    pkgdesc='Artix iso profiles base'

    _inst_dir 'shared'
    install -m644 $srcdir/${_profiles}-${pkgver}/repo_info $pkgdir/usr/share/artools/iso-profiles/
}

package_artix-iso-profiles() {
    pkgdesc='Artix iso profiles'
    groups=('artools')
    depends=("artools-yaml>=0.2")

    _inst_dir 'lxqt'
#     _inst_dir 'cinnamon'
}

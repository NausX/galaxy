# Maintainer: artoo <artoo@cromnix.org>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgbase=desktop-presets
pkgname=(
        'artix-icons'
        'artix-backgrounds'
        'artix-lxqt-basic-presets'
        'artix-lxqt-extra-presets'
        'artix-cinnamon-basic-presets'
        'artix-cinnamon-extra-presets'
)
pkgver=0.3.2
pkgrel=1
arch=('any')
url="https://github.com/artix-linux/desktop-presets"
license=('GPL')
makedepends=('git')
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
sha256sums=('706ce4ce5aeb4d786b59317731ecb5b61f2a18a3032a09c9f2f5ca18dbbc7b00')

_inst_dir(){
    cd ${pkgbase}-${pkgver}
    local profile="$1"

    install -d ${pkgdir}/etc
    cp -r $profile/skel ${pkgdir}/etc

    if [[ -d $profile/schemas ]];then
        install -d ${pkgdir}/usr/share/glib-2.0/schemas
        cp -r $profile/schemas ${pkgdir}/usr/share/glib-2.0/schemas
    fi

    if [[ -d $profile/scripts ]];then
        install -d ${pkgdir}/usr/bin
        cp -r $profile/scripts ${pkgdir}/usr/bin
    fi
}

package_artix-backgrounds(){
    pkgdesc='Artix backgrounds'

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_backgrounds
}

package_artix-icons(){
    pkgdesc='Artix icons'

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_icons
}

package_artix-lxqt-basic-presets() {
    pkgdesc='Artix lxqt basic presets'
    depends=('breeze-icons'
            'breeze-gtk'
            'artix-backgrounds'
            'artix-icons')
    provides=('desktop-presets')
    conflicts=('desktop-presets')

    _inst_dir 'lxqt/basic'
}

package_artix-lxqt-extra-presets() {
    pkgdesc='Artix lxqt extra presets'
    depends=('artix-lxqt-basic-presets')

    _inst_dir 'lxqt/extra'
}

package_artix-cinnamon-basic-presets() {
    pkgdesc='Artix cinnamon basic presets'
    depends=('artix-backgrounds'
            'artix-icons')
    provides=('desktop-presets')
    conflicts=('desktop-presets')

    _inst_dir 'cinnamon/basic'
}

package_artix-cinnamon-extra-presets() {
    pkgdesc='Artix cinnamon extra presets'
    depends=('artix-cinnamon-basic-presets')

    _inst_dir 'cinnamon/extra'
}

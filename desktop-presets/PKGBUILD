# Maintainer: artoo <artoo@cromnix.org>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgbase=desktop-presets
pkgname=(
        'artix-backgrounds'
        'artix-lxqt-basic-presets'
        'artix-lxqt-extra-presets'
        'artix-cinnamon-basic-presets'
        'artix-cinnamon-extra-presets'
)
pkgver=0.2
pkgrel=1
arch=('any')
url="https://github.com/artix-linux/desktop-presets"
license=('GPL')
makedepends=('git')
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
sha256sums=('0fad0dcf8fcb685ea5f1d8e36eaa7888d153a5c6f82ed70a4e948671c5a61b44')

_inst_dir(){
    cd ${pkgbase}-${pkgver}
    local profile="$1"

    install -d ${pkgdir}/etc
    cp -r $profile/skel ${pkgdir}/etc

    if [[ -d $profile/schemas ]];then
        install -d ${pkgdir}/usr/share/glib-2.0/schemas
        cp -r $profile/schemas ${pkgdir}/usr/share/glib-2.0/schemas
    fi

    if [[ -d $profile/scripts ]];then
        install -d ${pkgdir}/usr/bin
        cp -r $profile/scripts ${pkgdir}/usr/bin
    fi
}

package_artix-backgrounds(){
    pkgdesc='Artix backgrounds'

    cd ${pkgbase}-${pkgver}
    install -Dm755 backgrounds/artix.jpg ${pkgdir}/usr/share/backgrounds/artix.jpg
}

package_artix-lxqt-basic-presets() {
    pkgdesc='Artix lxqt basic presets'
    depends=('breeze-icons'
            'breeze-gtk'
            'artix-backgrounds')
    provides=('desktop-presets')
    conflicts=('desktop-presets')

    _inst_dir 'lxqt/basic'
}

package_artix-lxqt-extra-presets() {
    pkgdesc='Artix lxqt extra presets'
    depends=('artix-lxqt-basic-presets')

    _inst_dir 'lxqt/extra'
}

package_artix-cinnamon-basic-presets() {
    pkgdesc='Artix cinnamon basic presets'
    depends=('artix-backgrounds')
    provides=('desktop-presets')
    conflicts=('desktop-presets')

    _inst_dir 'cinnamon/basic'
}

package_artix-cinnamon-extra-presets() {
    pkgdesc='Artix cinnamon extra presets'
    depends=('artix-cinnamon-basic-presets')

    _inst_dir 'cinnamon/extra'
}

# Maintainer: Oscar Campos <damnwidget@artixlinux.org>

pkgname=prometheus
pkgver=1.7.1
pkgrel=1
pkgdesc="Monitoring system and time series database."
url="http://prometheus.io"
arch=('i686' 'x86_64')
license=('APACHE')
makedepends=('go' 'git')
backup=("etc/prometheus/prometheus.yml")
source=("https://github.com/prometheus/$pkgname/archive/v$pkgver.tar.gz"
	"${pkgname}.sysusers")
md5sums=('c90832267d4ec72bcb9bc291bdc22c96'
         'd47d5a96bd1f807447537fb644fb1a79')

prepare() {
  # create gopath and link sources
  msg2 'GOPATH setup'
  export GOPATH="$srcdir/gopath"
  mkdir -p "$GOPATH/src/github.com/prometheus/"
  ln -fsrT "$srcdir/$pkgname-$pkgver/" "$GOPATH/src/github.com/prometheus/$pkgname"
}

build() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/prometheus/$pkgname"
  make build
}

check() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/prometheus/$pkgname"
  make test
}

package() {
  install -Dm644 "$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  cd $pkgname-$pkgver
  install -Dsm755 prometheus "$pkgdir/usr/bin/$pkgname"
  install -Dsm755 promtool "$pkgdir/usr/bin/promtool"
  install -dm755 "$pkgdir/etc/$pkgname"
  install -dm750 "$pkgdir/var/lib/prometheus/"
  install -m644 "documentation/examples/prometheus.yml" "$pkgdir/etc/prometheus"
  install -dm755 "$pkgdir/usr/share/prometheus"
  cp -r consoles "$pkgdir/usr/share/prometheus"
  cp -r console_libraries "$pkgdir/usr/share/prometheus"
  ln -s /usr/share/prometheus/consoles "$pkgdir/etc/prometheus/consoles"
  ln -s /usr/share/prometheus/console_libraries "$pkgdir/etc/prometheus/console_libraries"
  install -dm755 "$pkgdir/usr/share/prometheus/web"
  cp -r web/* "$pkgdir/usr/share/prometheus/web"
}

# vim:set ts=2 sw=2 et:
